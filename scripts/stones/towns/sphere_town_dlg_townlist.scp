//****************************************************************************
// SPHERE by : Menasoft ©1997-2007
// www.sphereserver.com
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//
// DO NOT MAKE CHANGES TO DEFAULT SCRIPTS! MAKE YOUR ALTERATIONS IN CUSTOM FILES!
//****************************************************************************
// FILE LAST UPDATED: 14-March-2007
VERSION=0.56b

[dialog d_Town_TownList]
5,25
local.listtype=<args>
If (<local.listtype> <= 0)||(<local.listtype> > 4)
  return 1
elif (<type>!=t_stone_town)
  return 1
elif (<masteruid>!=<src.uid>)&&(!<src.isgm>)
  return 1
endif


page 0
resizepic 0 0 5054 550 440
resizepic 10 10 3000 530 420

local.privtolist=-1
local.showcheckbox=1
If ( <local.listtype>==<def0.TOWNSDLG_LISTTYPE_G_DECLAREPEACE> )
  local.privtolist=<def0.STONEPRIV_ENEMY>
  dhtmlgump 20 10 400 25 0 0 Select the town you wish to declare peace with.
  button 20 400 4005 4007 1 0 1
  dhtmlgump 55 400 245 30 0 0 Send the olive branch.
elif ( <local.listtype>==<def0.TOWNSDLG_LISTTYPE_G_ACCEPTWAR> )
  local.privtolist=<def0.STONEPRIV_THEYDECLARED>
  dhtmlgump 20 10 400 25 0 0 Select the town to accept the invitations: 
  button 20 400 4005 4007 1 0 1
  dhtmlgump 55 400 245 30 0 0 Accept war invitations.
elif ( <local.listtype>==<def0.TOWNSDLG_LISTTYPE_G_REJECTWAR> )
  local.privtolist=<def0.STONEPRIV_THEYDECLARED>
  dhtmlgump 20 10 400 25 0 0 Select the town to reject their invitations: 
  button 20 400 4005 4007 1 0 1
  dhtmlgump 55 400 245 30 0 0 Reject war invitations.
elif ( <local.listtype>==<def0.TOWNSDLG_LISTTYPE_G_RESCIND> )
  local.privtolist=<def0.STONEPRIV_WEDECLARED>
  dhtmlgump 20 10 400 25 0 0 Select the town to rescind our invitations: 
  button 20 400 4005 4007 1 0 1
  dhtmlgump 55 400 245 30 0 0 Rescind your war declarations.
endif
  
button 300 400 4005 4007 1 0 0
dhtmlgump 335 400 100 35 0 0 CANCEL

If ( <local.privtolist>==-1 )
  return 1
endif

local.current=0
local.currentgump=0
local.totalcount=<guild.count>
local.totalprivcount=<guild.count <local.privtolist>>

src.ctag.dialog_towntownlist_type=<local.listtype>
try src.ctag.dialog_towntownlist_<dlocal.listtype>_count=<local.totalprivcount>
try src.ctag.dialog_towntownlist_<dlocal.listtype>_radio=<local.showcheckbox>

while ( <local.currentgump> < <local.totalprivcount> )
  // ---- Condition for executing -----
  local.ifisok=<guild.<local.current>.uid>
  If ( <local.privtolist>==<def0.STONEPRIV_ENEMY> )
    local.ifisok=<local.ifisok> && <guild.<local.current>.isenemy>
  elif ( <local.privtolist>==<def0.STONEPRIV_THEYDECLARED> )
    local.ifisok=<local.ifisok> && <guild.<local.current>.theywar> && !<guild.<local.current>.wewar>
  elif ( <local.privtolist>==<def0.STONEPRIV_WEDECLARED> )
    local.ifisok=<local.ifisok> && <guild.<local.current>.wewar> && !<guild.<local.current>.theywar>
  endif
  // ----------------------------------
  
  If ( <local.ifisok> )
    // ----- Page ------
    If ( (<local.currentgump> % 11)==0 )
      If ( <local.currentgump> != 0 )
        button 300 370 4005 4007 0 <EVAL ((<local.currentgump>/11)+1)> 0
        dhtmlgump 335 370 300 35 0 0 Next page
      endif
        
      page <EVAL ((<local.currentgump>/11)+1)>
        
      If ( <local.currentgump> != 0 )
        button 20 370 4014 4016 0 <EVAL (<local.currentgump>/11)> 0
        dhtmlgump 335 370 300 35 0 0 1011067 Previous page
      endif
    endif
    // ----------------

    If ( <local.showcheckbox> )
      radio 20 <EVAL (35 + <EVAL ((<local.currentgump> % 11) * 30)>)> 208 209 0 <EVAL (<local.current> + 10)>
    endif
    
    local.currenttownuid=<guild.<local.current>.uid>  
    
    If (<eval strlen(<uid.<local.currenttownuid>.name>)>)
      dtext <QVAL <local.showcheckbox> ? 55 : 20> <EVAL (35 + <EVAL ((<local.currentgump> % 11) * 30)>)> 0 <uid.<local.currenttownuid>.name> <qval (strlen(<uid.<local.currenttownuid>.abbrev>))&&(<DEF0.TOWNSCONFIG_TOWNLIST_SHOWABBREV>) ? [<uid.<local.currenttownuid>.abbrev>]:>
    else
      dtext <QVAL <local.showcheckbox> ? 55 : 20> <EVAL (35 + <EVAL ((<local.currentgump> % 11) * 30)>)> 0 (Unnamed)
    endif

    local.currentgump += 1
  endif

  // ----- Total Member -----
  local.current += 1
  If ( <local.current> >= <local.totalcount> )
    local.currentgump=<local.totalprivcount>
  endif
  // ------------------------

endwhile


  
[dialog d_Town_TownList Button]
ON=0 1
  If (!<src.ctag0.dialog_towntownlist_type>)
    serv.log sdialog d_Town_TownList called without src.ctag.dialog_towntownlist_type !!
    return 1
  endif

  // ---- Var set ----
  local.listtype=<src.ctag0.dialog_towntownlist_type>
  local.listtypecount=<src.ctag0.dialog_towntownlist_<dLOCAL.listtype>_count>
  local.listtyperadio=<src.ctag0.dialog_towntownlist_<dLOCAL.listtype>_radio>
  src.ctag.dialog_towntownlist_type=
  try src.ctag.dialog_towntownlist_<dLOCAL.listtype>_count=
  try src.ctag.dialog_towntownlist_<dLOCAL.listtype>_radio=
  
  If (<masteruid>!=<src.uid>)&&(!<src.isgm>)
    return 1
  endif
  
  If (<argn1>==0) // Exit
    sdialog d_Town_Master_Warfare,0
    return 1  
  endif

  local.townuid=0
  If ( <local.listtyperadio> && <local.listtypecount> )
    local.selectedtown=<ARGCHKID> - 10
    If (<local.selectedtown> < 0)||(<local.selectedtown> > <guild.count>)
      return 1
    endif

    local.townuid=<guild.<local.selectedtown>.uid>
    If ( !<local.townuid> )
      return 1
    elif ( <uid.<local.townuid>.type>!=T_STONE_TOWN )
      return 1
    endIf  
  endif
  
  If ( <local.listtype>==<def0.TOWNSDLG_LISTTYPE_G_DECLAREPEACE> )
    If ( <f_townsys_isatwarwith <uid>,<local.townuid>> )
      f_townsys_declarepeace <uid>,<local.townuid>,1
    endif
  elif ( <local.listtype>==<def0.TOWNSDLG_LISTTYPE_G_ACCEPTWAR> )
    If ( <f_townsys_candeclarewarwith <uid>,<local.townuid>> )
      f_townsys_declarewar <uid>,<local.townuid>,1
    endif
  elif ( <local.listtype>==<def0.TOWNSDLG_LISTTYPE_G_REJECTWAR> )
    ref1 = <local.townuid>
    If ( <ref1.townfromuid.<uid>.wewar> )
      f_townsys_declarepeace <local.townuid>,<uid>,0
    endif
  elif ( <local.listtype>==<def0.TOWNSDLG_LISTTYPE_G_RESCIND> )
    If ( <guildfromuid.<local.townuid>.wewar> )
      f_townsys_declarepeace <uid>,<local.townuid>,0
    endif
  endif
  
  local.listtypecount -= 1
  If ( <local.listtypecount> )
    sdialog d_Town_TownList,0,<local.listtype>
  else
    sdialog d_Town_Master_Warfare,0
  endif
  return 1

[EOF]