// FILE LAST UPDATED: 17-Jan-2006 / RanX

[DEFNAME MINORS_SETTINGS] //these settings will only affect these artifacts, unless you add the event and tags to other items
USE_OSI_SPEEDCALCULATION 0 //1=yes,0=no
USE_OSI_DAMCALCULATION 1 //1=yes,0=no
minorartis {i_Alchemists_Bauble 1 i_Arctic_Death_Dealer 1 i_Blaze_Death 1 i_Bow_Juka_King 1 i_Burglar_Bandana 1 i_Candelabra_Souls 1 i_Captain_Quacklebush_Cutlass 1 i_Cavorting_Club 1 i_Cold_Blood 1 i_Enchanted_Titan_Leg_Bone 1 i_Ghost_Ship_Anchor 1 i_Gloves_Pugilist 1 i_Luna_Lance 1 i_Night_Kiss 1 i_Nox_Ranger_Heavy_Crossbow 1 i_Orcish_Visage 1 i_Pixie_Swatter 1 i_Polar_Bear_Mask 1 i_seahorse_statuette 1 Ship_Model_HMSCape 1 i_Wrath_Dryad 1 0241d 1 02851 1 02855 1 02a4b 1 i_fire_horn 1 i_sprinter_legs 1 i_spiked_chest 1 i_Heart_Lion 1 i_Art_Shield_Invul 1 i_Aegis_Grace 1 i_Boomstick 1 i_Bonesmasher 1 i_Raeds_Glory 1 i_Wildfire_Bow 1 i_Windsong 1 i_Talon_Bite 1 i_Soul_Seeker 1 i_Dreads_Revenge 1 i_Blade_Dance 1 i_Flesh_Ripper 1 i_Robe_Equinox 1 i_robe_Eclipse 1 i_Righteous_Anger 1 i_Bloodwood_Spirit 1 i_Brightsight_Lenses 1 i_Crimson_Cinture 1 i_Fey_Leggings 1 01beb 1 i_admirals_hearty_rum 1 i_dread_pirate_hat 1 i_Phillip_Wooden_Steed 1}
//this makes it easier if you want to add random artifacts
//Note: You should use the slayer_weapon scripts, although it's not necessary, but to have the weapons like they really are.

[TYPEDEF T_PROPRIEDADES]

ON=@ClientToolTip
	IF (<TAG0.DAMINCREASE>)
		SRC.ADDCLILOC 1060401,<EVAL <TAG0.DAMINCREASE>>
	endif
	IF (<TAG0.SWINGINCREASE>)
		SRC.ADDCLILOC 1060486,<EVAL <TAG0.SWINGINCREASE>>
	endif
	IF (<TAG0.HITCHANCEINCREASE>)
		SRC.ADDCLILOC 1060415,<EVAL <TAG0.HITCHANCEINCREASE>>
	endif
	IF (<TAG0.LIFELEECH>)
		SRC.ADDCLILOC 1060422,<EVAL <TAG0.LIFELEECH>>
	endif
	IF (<TAG0.MANALEECH>)
		SRC.ADDCLILOC 1060427,<EVAL <TAG0.MANALEECH>>
	endif
	IF (<TAG0.STAMLEECH>)
		SRC.ADDCLILOC 1060430,<EVAL <TAG0.STAMLEECH>>
	endif
	IF (<TAG0.HITDISPEL>)
		SRC.ADDCLILOC 1060417,<EVAL <TAG0.HITDISPEL>>
	endif
	IF (<TYPE>==T_ARMOR) || (<TYPE>==T_SHIELD) || (<TYPE>==T_ARMOR_LEATHER) || (<TYPE>==T_CLOTHING)
		IF (<TAG0.DAMAGERETURN>)
			SRC.ADDCLILOC 1060442,<EVAL <TAG0.DAMAGERETURN>>
		endif
		IF (<TAG0.HITCHANCEDECREASE>)
			SRC.ADDCLILOC 1042971,Hit Chance Decrease : <EVAL <TAG0.HITCHANCEDECREASE>>%
		endif
	ELIF (STRMATCH(*t_weapon*,<TYPE>))
		SRC.ADDCLILOC 1061167,<SPEED>
		IF (<TYPE>==T_WEAPON_SWORD)
			SRC.ADDCLILOC 1061172
		ELIF (<TYPE>==T_WEAPON_FENCE)
			SRC.ADDCLILOC 1061174
		ELIF (<TYPE>==T_WEAPON_MACE_SMITH) || (<TYPE>==T_WEAPON_MACE_SHARP)
			SRC.ADDCLILOC 1061173
		ELIF (<TYPE>==T_WEAPON_BOW)
			SRC.ADDCLILOC 1061175
		endif
	endif

ON=@Equip
	SRC.EVENTS +e_itens_com_props

ON=@UnEquip
	SRC.EVENTS -e_itens_com_props
	///////////////////////////////////

[EVENTS e_itens_com_props]

ON=@Hittry
	IF (<DEF.USE_OSI_SPEEDCALCULATION>)
		IF (<ACTION>!=SKILL_WRESTLING)
			ARGN1=<EVAL (((80000/((<STAM>+100)*<ARGO.SPEED>))/4)*10)-5>
		endif
	endif
	IF (<ARGO.TAG0.SWINGINCREASE>)
		ARGN1 -= <EVAL (<ARGN1>*<ARGO.TAG0.SWINGINCREASE>)/100>
	endif
	IF (<ARGO.TAG0.HITCHANCEINCREASE>)
		IF (<ACTDIFF> > 0)
			ACTDIFF += <EVAL (<ACTDIFF>*<ARGO.TAG0.SWINGINCREASE>)/100>
		ELSE
			ACTDIFF += <EVAL ((<ACTDIFF>*(-1))*<ARGO.TAG0.SWINGINCREASE>)/100>
		endif
	endif
	IF (<SRC.FINDLAYER.2.TYPE>==T_SHIELD)
		IF (<SRC.FINDLAYER.2.TAG0.HITCHANCEDECREASE>)
			IF (<ACTDIFF> > 0)
				ACTDIFF -= <EVAL (<ACTDIFF>*<SRC.FINDLAYER.2.TAG0.HITCHANCEDECREASE>)/100>
			endif
		endif
	endif
	IF (<ARGN1> < 10)
		ARGN1=10
	ELIF (<ARGN1> > 95)
		ARGN1=95
	endif

ON=@Hit
	IF (0<ARGO.UID>)
		LOCAL.TACTICSB=<EVAL (<TACTICS>/160)+1>
		LOCAL.ANATOMYB=<EVAL (<ANATOMY>/20)+5>
		LOCAL.LUMBERB=<EVAL <LUMBERJACKING>/50>
		LOCAL.STRB=<EVAL (<STR>*3)/10>
		LOCAL.BDAM=<R<ARGO.DAM>>
		LOCAL.FINALB=<EVAL ((((<LOCAL.TACTICSB>+<LOCAL.ANATOMYB>)+<LOCAL.LUMBERB>)+<LOCAL.STRB>)+<ARGO.TAG0.DAMINCREASE>)>
		ARGN1=<EVAL <LOCAL.BDAM>+((<LOCAL.BDAM>*<LOCAL.FINALB>)/100)>
		IF (<ARGO.TAG0.LIFELEECH>)
			HITS += <EVAL (<ARGN1>*<ARGO.TAG0.LIFELEECH>)/100>
			IF (<HITS> > <MAXHITS>)
				HITS=<MAXHITS>
			endif
		endif
		IF (<ARGO.TAG0.STAMLEECH>)
			STAM += <EVAL (<ARGN1>*<ARGO.TAG0.STAMLEECH>)/100>
			IF (<STAM> > <MAXSTAM>)
				STAM=<MAXSTAM>
			endif
		endif
		IF (<ARGO.TAG0.MANALEECH>)
			MANA += <EVAL (<ARGN1>*<ARGO.TAG0.MANALEECH>)/100>
			IF (<MANA> > <MAXMANA>)
				MANA=<MAXMANA>
			endif
		endif
		IF (<ARGO.TAG0.HITDISPEL>)
			IF (<R<ARGO.TAG0.HITDISPEL>> >= <R150>)
				SRC.SPELLEFFECT S_DISPEL,100.0
			endif
		endif
	endif
	///////////////////////////////////

[ITEMDEF i_Alchemists_Bauble]
ID=i_bracelet_gold
NAME=Alchemist's Bauble
WEIGHT=5
TEVENTS=t_luck_item
TAG.LUCK=30

ON=@Create
	COLOR=0823d

ON=@Equip
	SRC.MAGERY += 10.0
	SRC.ALCHEMY += 10.0

ON=@UnEquip
	SRC.MAGERY -= 10.0
	SRC.ALCHEMY -= 10.0

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,Magery,10
	SRC.ADDCLILOC 1060452,Alchemy,10

[ITEMDEF i_Arctic_Death_Dealer]
ID=i_mace_war
NAME=Arctic Death Dealer
WEIGHT=3.0
DAM=16,20
SPEED=30
REQSTR=80
SKILL=MACEFIGHTING
TAG.DAMINCREASE=40
TAG.SWINGINCREASE=20
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=100

ON=@Create
	HITPOINTS=255
	COLOR=0480

[ITEMDEF i_Blaze_Death]
ID=i_halberd
NAME=Blaze of Death
WEIGHT=3.0
DAM=18,21
TWOHANDS=Y
SPEED=25
REQSTR=80
SKILL=Swordsmanship
TAG.DAMINCREASE=35
TAG.SWINGINCREASE=25
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=100

ON=@Create
	COLOR=51
	HITPOINTS=255

[ITEMDEF i_Bow_Juka_King]
ID=i_bow
NAME=Bow of the Juka King
WEIGHT=2.5
DAM=16,18
SPEED=30
SKILL=Archery
TEVENTS=t_aos_weapon
REQSTR=30
TAG.DAMINCREASE=40
TAG.HITCHANCEINCREASE=15
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=80
TAG.SLAYERGROUP=REPTILE

ON=@Create
	COLOR=51
	HITPOINTS=255

[ITEMDEF i_Burglar_Bandana]
ID=i_bandana
NAME=Burglar's Bandana
REQSTR=10
TEVENTS=t_luck_item
TAG.LUCK=40

ON=@Equip
	SRC.SNOOPING += 10.0
	SRC.STEALING += 10.0
	SRC.STEALTH += 10.0
	SRC.MODDEX += 5

ON=@UnEquip
	SRC.SNOOPING -= 10.0
	SRC.STEALING -= 10.0
	SRC.STEALTH -= 10.0
	SRC.MODDEX -= 5

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,Snooping,10
	SRC.ADDCLILOC 1060452,Stealing,10
	SRC.ADDCLILOC 1060453,Stealth,10
	SRC.ADDCLILOC 1060409,5

[ITEMDEF i_Candelabra_Souls]
ID=i_candleabra2_lit
NAME=Candelabra of Souls
TYPE=T_NORMAL

ON=@Create
	COLOR=colors_all

[ITEMDEF i_Captain_Quacklebush_Cutlass]
ID=i_cutlass
NAME=Captain Quacklebush's Cutlass
WEIGHT=2.7
DAM=11,13
SPEED=44
REQSTR=30
SKILL=Swordsmanship
TAG.DAMINCREASE=50
TAG.SWINGINCREASE=20
TAG.HITCHANCEINCREASE=10
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=60

ON=@Create
	HITPOINTS=255
	COLOR=39

ON=@Equip
	SRC.MODDEX += 5

ON=@UnEquip
	SRC.MODDEX -= 5

ON=@ClientToolTip
	SRC.ADDCLILOC 1060409,5

[ITEMDEF i_Cavorting_Club]
ID=i_club
NAME=Cavorting Club
DAM=11,13
WEIGHT=3.0
SPEED=40
REQSTR=40
SKILL=Macefighting
TAG.DAMINCREASE=35
TAG.SWINGINCREASE=25
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=30

ON=@Create
	HITPOINTS=255
	COLOR=0305

ON=@Equip
	SRC.MODSTR += 3

ON=@UnEquip
	SRC.MODSTR -= 3

ON=@ClientToolTip
	SRC.ADDCLILOC 1060485,3

[ITEMDEF i_Cold_Blood]
ID=i_cleaver_meat
NAME=Cold Blood
DAM=11,13
SPEED=46
REQSTR=10
SKILL=Swordsmanship
TAG.SWINGINCREASE=40
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=30

ON=@Create
	HITPOINTS=255
	COLOR=08120

ON=@Equip
	SRC.MAXHITS += 6
	SRC.MAXMANA += 6
	SRC.MAXSTAM += 6

ON=@UnEquip
	SRC.MAXHITS -= 6
	SRC.MAXMANA -= 6
	SRC.MAXSTAM -= 6

ON=@ClientToolTip
	SRC.ADDCLILOC 1060431,6
	SRC.ADDCLILOC 1060439,6
	SRC.ADDCLILOC 1060484,6

[ITEMDEF i_Enchanted_Titan_Leg_Bone]
ID=i_spear_short
NAME=Enchanted Titan Leg Bone
DAM=10,13
SPEED=55
REQSTR=10
SKILL=Fencing
TAG.DAMINCREASE=20
TAG.HITCHANCEINCREASE=10
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=50

ON=@Create
	HITPOINTS=255
	COLOR=59

[ITEMDEF i_Ghost_Ship_Anchor]
ID=i_anchor
NAME=Ghost Ship Anchor

ON=@Create
	COLOR=color_rock

[ITEMDEF i_Gloves_Pugilist]
ID=i_leather_gloves
NAME=Gloves of the Pugilist
REQSTR=20

ON=@Create
	HITPOINTS=255
	COLOR=color_o_rusty

ON=@Equip
	SRC.WRESTLING += 10.0
	SRC.MODDEX += 8

ON=@UnEquip
	SRC.WRESTLING -= 10.0
	SRC.MODDEX -= 8

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,Wrestling,10
	SRC.ADDCLILOC 1060409,8

[ITEMDEF i_Luna_Lance]
ID=i_lance
NAME=Luna Lance
DAM=17,20
SPEED=28
REQSTR=90
SKILL=Fencing
TAG.DAMINCREASE=35
TAG.SWINGINCREASE=20
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=100

ON=@Create
	HITPOINTS=255
	COLOR=0481

ON=@Equip
	SRC.CHIVALRY += 10.0
	SRC.MODSTR += 5

ON=@UnEquip
	SRC.CHIVALRY -= 10.0
	SRC.MODSTR -= 5

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,Chivalry,10
	SRC.ADDCLILOC 1060485,5

[ITEMDEF i_Night_Kiss]
ID=i_dagger
NAME=Night's Kiss
DAM=9,11
SPEED=55
REQSTR=10
SKILL=Fencing
TAG.DAMINCREASE=35
TAG.SWINGINCREASE=30
TAG.LIFELEECH=40
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TEVENTS=T_AOS_WEAPON
TAG.SLAYERGROUP=REPOND
TAG.LUCK=45

ON=@Create
	HITPOINTS=255
	COLOR=color_o_shadow

[ITEMDEF i_Nox_Ranger_Heavy_Crossbow]
ID=i_crossbow_heavy
NAME=Nox Ranger's Heavy Crossbow
DAM=19,20
TWOHANDS=Y
SPEED=22
REQSTR=80
SKILL=Archery
TAG.DAMINCREASE=20
TAG.SWINGINCREASE=30
TAG.STAMLEECH=40
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=45

ON=@Create
	HITPOINTS=255
	COLOR=0369

[ITEMDEF i_Orcish_Visage]
ID=i_mask_orc
NAME=Orcish Visage
REQSTR=30
TEVENTS=t_luck_item
TAG.LUCK=30

ON=@Create
	HITPOINTS=255
	COLOR=02a6

ON=@Equip
	src.tag.origdex=<src.dex>
	SRC.MODSTR += 10
	SRC.MAXSTAM += 5

ON=@UnEquip
	SRC.MODSTR -= 10
	SRC.MAXSTAM -= (5 - <src.dex> + <src.tag.origdex>)
	src.tag.origdex =


ON=@ClientToolTip
	SRC.ADDCLILOC 1060484,5
	SRC.ADDCLILOC 1060485,10

[ITEMDEF i_Pixie_Swatter]
ID=i_scepter
NAME=Pixie Swatter
DAM=14,17
TWOHANDS=N
SPEED=30
REQSTR=40
SKILL=Macefighting
TAG.DAMINCREASE=20
TAG.SWINGINCREASE=30
TAG.STAMLEECH=40
TEVENTS=T_AOS_WEAPON
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=25
TAG.SLAYERGROUP=FEY

ON=@Create
	HITPOINTS=255
	COLOR=33

[ITEMDEF i_Polar_Bear_Mask]
ID=i_mask_bear
NAME=Polar Bear Mask
REQSTR=30
TEVENTS=t_luck_item
TAG.LUCK=40

ON=@Create
	HITPOINTS=255
	COLOR=0481

ON=@Equip
	TIMER=5
	SRC.SPELLEFFECT s_night_sight,1000

ON=@Timer
	IF (<CONT.ISPLAYER>)
		CONT.HITS += 2
		IF (<CONT.HITS> > <CONT.MAXHITS>)
			CONT.HITS=<CONT.MAXHITS>
		endif
		TIMER=5
		RETURN 1
	endif

ON=@ClientToolTip
	SRC.ADDCLILOC 1060444,2
	SRC.ADDCLILOC 1060485,10
	SRC.ADDCLILOC 1015008

[ITEMDEF 9658]
DEFNAME=i_seahorse_statuette
NAME=Sea Horse

[ITEMDEF Ship_Model_HMSCape]
ID=i_model_ship
NAME=Ship Model of the H.M.S. Cape

[ITEMDEF i_Wrath_Dryad]
ID=i_staff_gnarled
NAME=Wrath of the Dryad
DAM=15,17
TWOHANDS=Y
SPEED=33
REQSTR=20
SKILL=Macefighting
TAG.DAMINCREASE=40
TAG.HITCHANCEINCREASE=15
TAG.MANALEECH=50
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=110

ON=@Create
	HITPOINTS=255
	COLOR=0ad

[itemdef i_corredor_sword]
ID=i_paladin_sword
DEFNAME=i_corredor_sword
NAME=The Great Sword
TYPE=t_weapon_sword
SKILL=swordsmanship
SPEED=30
WEIGHT=3.1
DAM=15,35
TEVENTS=t_aos_weapon
TEVENTS=T_PROPRIEDADES
TAG.DAMINCREASE=30
TAG.HITCHANCEINCREASE=60
TEVENTS=t_luck_item
TAG.LUCK=70

ON=@create
	hitpoints=252
	morem=2
	COLOR=35

[ITEMDEF i_fire_horn]
ID=0fc7
NAME=Fire Horn
VALUE=5000
WEIGHT=6

ON=@Create
	COLOR=0f8

ON=@Dclick
	IF (<CONT>)
		IF (<TOPOBJ.UID>==<SRC>)
			IF !(<SRC.FINDID.i_fire_horn_TIMER>)
				IF (<SRC.RESTEST 4 i_reag_sulfur_ash>)
					IF (<SRC.MUSICIANSHIP> >= 52.6)
						SRC.CONSUME 4 i_reag_sulfur_ash
						FORCHARS <EVAL (<SRC.Musicianship>/100)+(<SRC.Provocation>/100)>
							IF (<UID>!=<SRC>)
								IF (<CANSEELOS>)
									LOCAL.DIV=<QVAL (<ISPLAYER>) ? 110:80>
									DAMAGE <EVAL ((<SRC.Musicianship>/<LOCAL.DIV>)+(<SRC.Provocation>/<LOCAL.DIV>)+(<SRC.Peacemaking>/<LOCAL.DIV>)+(<SRC.Enticement>/<LOCAL.DIV>))-5> 02 <SRC>
								endif
							endif
						end
						SERV.NEWITEM=i_fire_horn_TIMER
						NEW.CONT=<SRC>
						NEW.TIMER=7
					endif
				ELSE
					SRC.SYSMESSAGE Voce nao tem 4 Sulfurous Ash.
				endif
			ELSE
				SRC.SYSMESSAGE Aguarde mais <SRC.FINDID.i_fire_horn_TIMER.TIMER> segundos.
			endif
		endif
	endif
	RETURN 1

[ITEMDEF i_fire_horn_TIMER]
ID=i_memory
TYPE=T_EQ_SCRIPT
NAME=Fire horn timer

ON=@Timer
	REMOVE
	RETURN 1

[ITEMDEF i_sprinter_legs]
DEFNAME=i_sprinter_legs
NAME=Sprinter Legs
TYPE=t_armor_leather
ID=i_Elven_armor_plate_legs
ARMOR=80
REQSTR=25
TEVENTS=T_PROPRIEDADES
TAG.DAMAGERETURN=10
TEVENTS=t_luck_item
TAG.LUCK=65
WEIGHT=11.5

ON=@CREATE
	COLOR=colors_all
	HITPOINTS=255

ON=@Equip
	SRC.MODDEX += 15

ON=@UnEquip
	SRC.MODDEX -= 15

ON=@ClientToolTip
	SRC.ADDCLILOC 1060409,15

[ITEMDEF i_spiked_chest]
DEFNAME=i_spiked_chest
NAME=Spiked Chest
ID=i_platemail_chest
TYPE=t_armor
ARMOR=90
REQSTR=65
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=55
TAG.DAMAGERETURN=20
WEIGHT=11.5

ON=@CREATE
	COLOR=colors_brown
	HITPOINTS=255

ON=@Equip
	SRC.MODSTR += 10

ON=@UnEquip
	SRC.MODSTR -= 10

ON=@ClientToolTip
	SRC.ADDCLILOC 1060485,10

[ITEMDEF i_Heart_Lion]
DEFNAME=i_Heart_Lion
NAME=Heart of the Lion
ID=i_platemail_chest
TYPE=t_armor
ARMOR=80
REQSTR=65
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.DAMAGERETURN=10
TAG.LUCK=95
WEIGHT=11.5

ON=@CREATE
	COLOR=0fc
	HITPOINTS=255

ON=@Equip
	SRC.MODSTR += 5

ON=@UnEquip
	SRC.MODSTR -= 5

ON=@ClientToolTip
	SRC.ADDCLILOC 1060485,5

[ITEMDEF i_Art_Shield_Invul]
DEFNAME=i_Art_Shield_Invul
NAME=Shield of Invulnerability
ID=i_shield_order
TYPE=t_shield
ARMOR=15
REQSTR=65
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.DAMAGERETURN=10
TAG.LUCK=45
WEIGHT=9.5

ON=@CREATE
	COLOR=0bc
	HITPOINTS=255

[ITEMDEF i_Aegis_Grace]
DEFNAME=i_Aegis_Grace
NAME=Aegis Of Grace
ID=i_dragon_helm
ARMOR=100
REQSTR=65
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.DAMAGERETURN=10
TAG.LUCK=25
WEIGHT=6.0

ON=@CREATE
	HITPOINTS=255

ON=@Equip
	SRC.MAGICRESISTANCE += 10.0

ON=@UnEquip
	SRC.MAGICRESISTANCE -= 10.0

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,MagicResistance,10

[ITEMDEF i_Boomstick]
DEFNAME=i_Boomstick
NAME=Boomstick
ID=i_mace_wildstaff
TWOHANDS=N
TEVENTS=T_PROPRIEDADES
TAG.SWINGINCREASE=15
TAG.DAMINCREASE=25

ON=@CREATE
	HITPOINTS=255
	COLOR=39

ON=@Equip
	TIMER=5

ON=@Dropon_Ground
	IF (((<UID.<REGION.UID>.TYPE>==T_MULTI) || (<UID.<REGION.UID>.TYPE>==T_SHIP)) && (<UID.<REGION.UID>.MORE1>==<SRC>))
		TIMER -1
	ELSE
		ATTR |= ATTR_DECAY
		TIMER=<EVAL <SERV.DECAYTIMER>*60>
	endif

ON=@Timer
	IF (<CONT.ISPLAYER>)
		CONT.MANA += 3
		IF (<CONT.MANA> > <CONT.MAXMANA>)
			CONT.MANA=<CONT.MAXMANA>
		endif
		TIMER=5
		RETURN 1
	endif

ON=@ClientToolTip
	SRC.ADDCLILOC 1060440,3

[ITEMDEF i_Bonesmasher]
DEFNAME=i_Bonesmasher
NAME=Bonesmasher
ID=i_mace_diamondmace
TEVENTS=T_PROPRIEDADES
TAG.STAMLEECH=40

ON=@CREATE
	HITPOINTS=255

ON=@Equip
	SRC.MACEFIGHTING += 10.0

ON=@UnEquip
	SRC.MACEFIGHTING -= 10.0

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,Macefighting,10

[ITEMDEF i_Raeds_Glory]
ID=11555
DEFNAME=i_Raeds_Glory
NAME=Raed's Glory
TEVENTS=T_PROPRIEDADES
TAG.LIFELEECH=40
TAG.SWINGINCREASE=20
TAG.DAMINCREASE=15

ON=@CREATE
	COLOR=024f
	HITPOINTS=255

ON=@Equip
	SRC.MAXMANA += 8

ON=@UnEquip
	SRC.MAXMANA -= 8

ON=@ClientToolTip
	SRC.ADDCLILOC 1060439,8

[ITEMDEF i_Wildfire_Bow]
ID=i_elven_composite_longbow
DEFNAME=i_Wildfire_Bow
NAME=Wildfire Bow

ON=@CREATE
	TAG0.ORB.EFEITO |= <DEF.DAM_FIRE>
	COLOR=054f
	HITPOINTS=255

ON=@Equip
	SRC.ARCHERY += 100

ON=@UnEquip
	SRC.ARCHERY -= 100

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,Archery,10

[ITEMDEF i_Windsong]
ID=i_magical_shortbow
DEFNAME=i_Windsong
NAME=Windsong
TEVENTS=T_PROPRIEDADES
TAG.DAMINCREASE=35
TAG.SWINGINCREASE=25

ON=@CREATE
	COLOR=0558
	HITPOINTS=255

[ITEMDEF i_Talon_Bite]
DEFNAME=i_Talon_Bite
NAME=Talon Bite
ID=i_ornate_axe
TEVENTS=T_PROPRIEDADES
TAG.DAMINCREASE=35
TAG.SWINGINCREASE=20

ON=@CREATE
	HITPOINTS=255

ON=@Equip
	SRC.TACTICS += 10.0

ON=@UnEquip
	SRC.TACTICS -= 10.0

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,Tactics,10

[ITEMDEF i_Soul_Seeker]
ID=i_Radiant_Scimitar
DEFNAME=i_Soul_Seeker
NAME=Soul Seeker
TEVENTS=T_PROPRIEDADES
TEVENTS=T_AOS_WEAPON
TAG.LIFELEECH=40
TAG.MANALEECH=40
TAG.STAMLEECH=40
TAG.SWINGINCREASE=60
TAG.SLAYERGROUP=REPOND

ON=@CREATE
	HITPOINTS=255
	COLOR=03e1

[ITEMDEF i_Blade_Dance]
ID=i_Rune_Blade
DEFNAME=i_Blade_Dance
NAME=Blade Dance
TEVENTS=T_PROPRIEDADES
TAG.MANALEECH=20
TAG.DAMGINCREASE=30

ON=@CREATE
	HITPOINTS=255
	COLOR=color_o_bloodrock

ON=@Equip
	SRC.MAXMANA += 8

ON=@UnEquip
	SRC.MAXMANA -= 8

ON=@ClientToolTip
	SRC.ADDCLILOC 1060439,8

[ITEMDEF i_Dreads_Revenge]
ID=i_kryss
DEFNAME=i_Dreads_Revenge
NAME=Dread's Revenge

ON=@CREATE
	HITPOINTS=255
	COLOR=04f8

ON=@Equip
	SRC.FENCING += 20.0

ON=@UnEquip
	SRC.FENCING -= 20.0

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,Fencing,20

[ITEMDEF i_Flesh_Ripper]
ID=i_sword_assassin_spike
DEFNAME=i_Flesh_Ripper
NAME=Flesh Ripper
TEVENTS=T_PROPRIEDADES
TAG.HITCHANCEINCREASE=15
TAG.SWINGINCREASE=40

ON=@CREATE
	HITPOINTS=255
	COLOR=color_o_copper

ON=@Equip
	SRC.MODSTR += 5
	SRC.ANATOMY += 10.0

ON=@UnEquip
	SRC.MODSTR -= 5
	SRC.ANATOMY -= 10.0

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,Anatomy,10
	SRC.ADDCLILOC 1060485,5

[ITEMDEF i_Robe_Equinox]
ID=i_robe
DEFNAME=i_Robe_Equinox
NAME=Robe of the Equinox
TEVENTS=T_LUCK_ITEM
TAG.LUCK=95

ON=@CREATE
	HITPOINTS=255
	COLOR=026c

ON=@EquiTest
	IF (<BODY>!=C_ELF_MALE) && (<BODY>!=C_ELF_FEMALE)
		MESSAGE @0481,3,1 Apenas elfos podem usar.
		RETURN 1
	endif

[ITEMDEF i_Robe_Eclipse]
ID=i_robe
DEFNAME=i_Robe_Eclipse
NAME=Robe of the Eclipse
TEVENTS=T_LUCK_ITEM
TAG.LUCK=95

ON=@CREATE
	HITPOINTS=255
	COLOR=0486

[ITEMDEF i_Righteous_Anger]
ID=i_elven_machete
DEFNAME=i_Righteous_Anger
NAME=Righteous Anger
TEVENTS=T_PROPRIEDADES
TAG.HITCHANCEINCREASE=15
TAG.DAMINCREASE=40
TAG.SWINGINCREASE=35

ON=@CREATE
	HITPOINTS=255
	COLOR=color_o_copper

[ITEMDEF i_Bloodwood_Spirit]
ID=i_talisman3
DEFNAME=i_Bloodwood_Spirit
NAME=Bloodwood Spirit
TEVENTS=T_LUCK_ITEM
TAG.LUCK=50

ON=@CREATE
	COLOR=33

ON=@Equip
	SRC.SPIRITSPEAK += 100

ON=@UnEquip
	SRC.SPIRITSPEAK -= 100

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,Spirit Speak,10

[ITEMDEF i_Brightsight_Lenses]
ID=i_elven_specs
DEFNAME=i_Brightsight_Lenses
NAME=Brightsight Lenses

ON=@CREATE
	HITPOINTS=255
	COLOR=04e8

ON=@Equip
	TIMER=5
	SRC.SPELLEFFECT S_NIGHT_SIGHT,1000

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,Spirit Speak,10

ON=@Timer
	IF (<CONT.ISPLAYER>)
		CONT.MANA += 3
		IF (<CONT.MANA> > <CONT.MAXMANA>)
			CONT.MANA=<CONT.MAXMANA>
		endif
		TIMER=4
		RETURN 1
	endif

ON=@Dropon_Ground
	IF (((<UID.<REGION.UID>.TYPE>==T_MULTI) || (<UID.<REGION.UID>.TYPE>==T_SHIP)) && (<UID.<REGION.UID>.MORE1>==<SRC>))
		TIMER -1
	ELSE
		ATTR |= ATTR_DECAY
		TIMER=<EVAL <SERV.DECAYTIMER>*60>
	endif

[ITEMDEF i_Crimson_Cinture]
ID=i_talisman4
DEFNAME=i_Crimson_Cinture
NAME=Crimson Cinture

ON=@CREATE
	COLOR=33

ON=@Equip
	TIMER=5
	SRC.MODDEX += 5
	SRC.MAXHITS += 10

ON=@UnEquip
	SRC.MODDEX -= 5
	SRC.MAXHITS -= 10

ON=@ClientToolTip
	SRC.ADDCLILOC 1060451,Spirit Speak,10

ON=@Timer
	IF (<CONT.ISPLAYER>)
		CONT.HITS += 2
		IF (<CONT.HITS> > <CONT.MAXHITS>)
			CONT.HITS=<CONT.MAXHITS>
		endif
		TIMER=5
		RETURN 1
	endif

ON=@Dropon_Ground
	IF (((<UID.<REGION.UID>.TYPE>==T_MULTI) || (<UID.<REGION.UID>.TYPE>==T_SHIP)) && (<UID.<REGION.UID>.MORE1>==<SRC>))
		TIMER -1
	ELSE
		ATTR |= ATTR_DECAY
		TIMER=<EVAL <SERV.DECAYTIMER>*60>
	endif

[ITEMDEF i_Fey_Leggings]
DEFNAME=i_Fey_Leggings
NAME=Fey Leggings
ID=i_chainmail_leggings
ARMOR=130
TEVENTS=T_PROPRIEDADES
TAG.DAMAGERETURN=6

ON=@CREATE
	HITPOINTS=255

ON=@EquipTest
	IF (<SRC.OBODY>!=C_ELF_MALE) && (<SRC.OBODY>!=C_ELF_FEMALE) && !(<SRC.ISGM>)
		MESSAGE @03c1,3,1 Apenas elfos.
		RETURN 1
	endif

ON=@Equip
	SRC.MAXHITS += 6

ON=@UnEquip
	SRC.MAXHITS -= 6

ON=@ClientToolTip
	SRC.ADDCLILOC 1060431,6
	SRC.ADDCLILOC 1075086

[ITEMDEF i_admirals_hearty_rum]
ID=i_bottle_liquor
NAME=The Admiral's Hearty Rum
TYPE=T_NORMAL

ON=@Create
	COLOR=33

[ITEMDEF i_Phillip_Wooden_Steed]
ID=16382
NAME=Phillip's Wooden Steed
TYPE=T_NORMAL

[ITEMDEF i_dread_pirate_hat]
ID=i_hat_tricorne
NAME=Dread Pirate Hat
WEIGHT=3
REQSTR=10

ON=@Create
	HITPOINTS=255
	COLOR=colors_brown
	TAG.SKILL=<hval {SKILL_WRESTLING 1 SKILL_SWORDSMANSHIP 1 SKILL_FENCING 1 SKILL_PARRYING 1 SKILL_MACEFIGHTING 1}>

ON=@Equip
	SRC.spelleffect s_night_sight,1000,<SRC>
	try SRC.<SERV.SKILL.<TAG0.SKILL>.KEY>=<hval <SRC.<SERV.SKILL.<TAG0.SKILL>.KEY>>+100>

ON=@UnEquip
	try SRC.<SERV.SKILL.<TAG0.SKILL>.KEY>=<hval <SRC.<SERV.SKILL.<TAG0.SKILL>.KEY>>-100>

ON=@ClientToolTip
	SRC.addcliloc 1060451,<SERV.SKILL.<TAG0.SKILL>.KEY>,10
	SRC.addcliloc 1015008
